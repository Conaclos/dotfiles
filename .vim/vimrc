"
" Copyright (c) 2015-2018 Victorien Elvinger
" Licensed under the zlib license (https://opensource.org/licenses/zlib).
"


set nocompatible
    " Don't care about retro-compatibility of Vi settings
    " Must be first since it has a side-effect on the settings

set history=100 " Command history length (Default: 50)

set textwidth=80 " Ideal maximum width of a line

set formatoptions-=t " No automatic insertion of newline

set autoread " Automatically reload if file was changed outside


" XDG compliant
" -------------

if empty($XDG_DATA_HOME)
    let $VIM_DATA_HOME=$HOME.'/.local/share/vim'
else
    let $VIM_DATA_HOME=$XDG_DATA_HOME.'/vim'
endif

set runtimepath+=$VIM_DATA_HOME " Add $VIM_DATA_HOME as runtime directory


" Backup
" ------

if isdirectory($VIM_DATA_HOME.'/backup')
    set backupdir=$VIM_DATA_HOME/backup/

    set backupskip+=/tmp/*,*/shm/*,*/playroom/*,*.swp,*.bak
else
    set nobackup
    set nowritebackup " Enable saving without backup
endif


if isdirectory($VIM_DATA_HOME.'/swap')
    set directory=$VIM_DATA_HOME/swap// " // prevents name collision
else
    set noswapfile
endif


if has('persistent_undo') && isdirectory($VIM_DATA_HOME.'/undo')
    " Persistent undo

    set undodir=$VIM_DATA_HOME/undo/
    set undofile " Enable persistent undo files
    set undolevels=200 " Maximum number of undoing changes
    set undoreload=8000 " Maximum number of lines to save for undo
endif


set viminfo=
    " Skip other histories


" Colors
" ------

colorscheme Tomorrow-Night
set background=dark " Adapt colors to a dark background

syntax on " Syntax highlighting


" UI
" --

set lazyredraw " Redraw only when needed

set scrolloff=10 " Minimal number of lines to keep above and below the cursor
set sidescrolloff=10 " Minimal number of columns to keep horizontally

set nostartofline " Leave the cursor where it was for a bunch of commands

set matchtime=2 " How many tenths of a second to blink when matching brackets

set nowrap " No line wrapping

set iskeyword=_,#,$,@,% " Are not word separators

set colorcolumn=+1 " Highlighting column "textwidth"+1
highlight ColorColumn ctermbg=darkgrey guibg=darkgrey

set cursorline " Highlight line where the cursor is

set showcmd " Show partial commands


" Line numbering
" --------------

set number " Absolute line numbering
set relativenumber " Relative line numbering

autocmd InsertEnter * :set norelativenumber
autocmd InsertLeave * :set relativenumber


" Status-line
" -----------

set laststatus=2 " Display the status line always

set statusline=%f " Relative path to the file
set statusline+=%= " Switch to the right side
set statusline+=%-12.{Paste_mode()}
set statusline+=%-10.y " File type as recognized
set statusline+=%-8.{Indentation_practice()} " Indentation practice
set statusline+=%-6.(%c%V%) " Current Column
set statusline+=%-4.P " Percentage through file


function! Indentation_practice()
    if &expandtab == 1
        return "Sp".&shiftwidth
    else
        return "Tab"
    endif
endfunction

function! Paste_mode()
    if &paste == 1
        return "Paste"
    else
        return ""
    endif
endfunction


" Searching
" ---------

set hlsearch " Highlight all matches for the pattern
set incsearch " Show the first match for the pattern, while typing

set ignorecase " Case-insensitive search
set smartcase " Use case-sensitive search when a capital is present


" Auto-completion
" ---------------

set complete+=kspell " Spell auto-completion when spell is enabled
set infercase " Case-sensitive auto-completion
set wildmenu " Command-line <Tab> completion

function! Insert_tab_completion()
    " Do completion when possible. Else, indent.

    let col = col('.') - 1
    if !col || getline('.')[col - 1] !~ '\k'
        return "\<tab>"
    else
        return "\<c-p>"
    endif
endfunction

inoremap <expr> <tab> Insert_tab_completion()


" Indentation and whitespace
" --------------------------

filetype plugin indent on

set autoindent
    " When opening a new line and no file-type-specific indenting is enabled,
    " keep the same indent as the previous line

set copyindent " Copy indetation of the previous line
set smartindent " Better auto-indentation for C-like syntax

set shiftround " Absorb space when tab is used
set shiftwidth=4 " Number of inserted columns by the indent operation
set softtabstop=4 " Number of inserted columns when <Tab> is hit for indenting
set tabstop=4 " Number of columns for a Tab


set list listchars=tab:» ,trail:•,nbsp:·
    " Show tabs, trailing and non-breaking spaces

highlight SpecialKey term=bold ctermfg=red guifg=red
    " Color for "nbsp", "space", "tab", and "trail" from "listchars"


set expandtab

function! Infer_indentation_practice()
    " Infer indentation practice (tabs or 2/4 spaces)
    " based on first 800 lines

    let header=getline(1,800)

    if match(header,"^\t", 0, 1) != -1
        " Use tabs for indenting

        set noexpandtab
        set smarttab " Use tabs for indentation and spaces for alignment

    elseif match(header,"^[ ][ ][^ ]", 0, 1) != -1
        " Use 2 spaces for indenting

        set shiftwidth=2
        set softtabstop=2
    else
        " Use 4 spaces for indenting
    endif
endfunction

autocmd BufReadPost * call Infer_indentation_practice()
    " Infer indentation practice when opening a file (before "modelines")


" key mapping
" -----------

nnoremap Y y$
    " default: "Y" acts as "yy"
    " Same behavior as D and C

set pastetoggle=<F2>
    " Toggle between nopaste and paste mode
    " Paste mode prevents indenting

vmap <C-c> y:call system("xclip -i -selection clipboard", getreg("\""))<CR>:call system("xclip -i", getreg("\""))<CR>
    " Copy to clipboard
nmap <C-v> :call setreg("\"",system("xclip -o -selection clipboard"))<CR>p
    " Paste from clipboard

cnoremap <expr> %% expand('%:h').'/' " %% shortcut to insert file's dir


" POSIX regex
" -----------

nnoremap / /\v
vnoremap / /\v
nnoremap :g/ :g/\v


" File type
" ---------

filetype on
autocmd BufNewFile,BufRead *.md set filetype=markdown
autocmd BufNewFile,BufRead *.rs set filetype=rust
autocmd BufNewFile,BufRead *.ts,*.tsx set filetype=typescript

